#Sophia [Sapphire] - BNF Grammar Definition v1.7.1
(* =========================================================
	Sophia (AI₂O₃) - BNF Grammar Definition v1.7.1
	"Integrated Physicality" - Full Unified Version
	========================================================= *)

(* =========================================================
	PROGRAM STRUCTURE
	========================================================= *)

<Program>		::= <Definition>*

<Definition>	::= <SchemaDef>   (* データ構造：# *)
				| <ConstDef>    (* 定数定義：% *)
				| <TaskDef>     (* 副作用あり関数：& *)
				| <PureDef>     (* 純粋関数：! *)
				| <SceneDef>    (* 物理スコープ：scene *)

(* =========================================================
	SCENE & LENS MODEL (2文字レンズ + ブロック)
	========================================================= *)

<SceneDef>		::= "scene" <Ident> <Lens> <BodyBlock>

(* レンズ：物理的な透過特性。直後にブロック構造 [] が続く *)
<Lens>			::= "]>"  (* Wide Lens: 基底レイヤー / 全域透過 *)
				| "|>"  (* Normal Lens: メインレイヤー / 標準集光 *)
				| "[>"  (* Tele Lens: サブレイヤー / 拡大・透過 *)
(*== EventDef allow only in BobyBlock of SceneDef  ==*)
<EventDef>		::= "on" <Ident> <ArgParamList>? <BodyBlock>
<ArgParamList>	::= "[" ( "." <Ident> ( "," "." <Ident> )* )? "]"

(* =========================================================
	DATA & CONSTANT DEFINITIONS
	========================================================= *)

<SchemaDef>		::= "#" <Ident> "[" <FieldDef>* "]" <Capacity>?
<FieldDef>		::= <Ident> ":" <Type>
<Type>			::= "i8" | "i16" | "i32" | "u8" | "u16" | "u32" 
				| "f32" | "f64" | "str" | "bool" | "bit"
				| <Ident> | <Type> "*" <Int>
<Capacity>		::= "[" <Int> "]"

<ConstDef>		::= "%" <Ident> "[" <HashList> "]"
<HashList>		::= <HashPair> ( "," <HashPair> )*
<HashPair>		::= <Ident> ( "::" <Expression> )?

(* =========================================================
	LVALUE & NATURE (物理参照パス)
	========================================================= *)

<LValue>		::= <Nature> <Ident> <SubAccess>*

<Nature>		::= "." (* Constant / Argument: 定数および引数 (不変) *)
				| "%" (* Hash: ハッシュマップ / 名前付き定数群 *)
				| "~" (* Mutable: 可変変数 (Scope内) *)
				| "#" (* Table: 構造化データ実体 (TypedArray) *)
				| "@" (* Array: 物理配列 / システム状態 *)
				| "$" (* Sapphire: 外部I/Oゲート/システムコール *)

<SubAccess>		::= "@" <Expression> 
				| "#" <Expression> 
				| "%" <Ident>

(* =========================================================
	FUNCTION & EXECUTION UNITS
	========================================================= *)

<TaskDef>		::= "&" <Ident> <ParamList>? <BodyBlock>
<PureDef>		::= "!" <Ident> <ParamList>? <BodyBlock>
<ParamList>		::= "[" ( <Ident> ( "," <Ident> )* )? "]"

<BodyBlock> ::= <BlockPrefix>? "[" <Statement>* <ExitControl>? "]"
<BlockPrefix> ::= "!" | "&"


<Statement>		::= <IterateBlock>
				| <QAChain>
				| <Action>
				| <PureCall>
				| <TaskCall>
				| <SapphireCall>
				| <EventDef> (* Only in Scene BodyBlock *)
				| <ExitControl>

(* =========================================================
	FLOW CONTROL (QA, TRY, EXIT)
	========================================================= *)

<QAChain>		::= <QPrefix> <BodyBlock> ( <ElseIfBlock> | <ElseBlock> )?

<QPrefix>		::= "?" "[" <Expression> "]"         (* If: 条件 *)
				| "?!"                               (* Try: 成否 *)

<ElseIfBlock>	::= "|?" <QAChain>
<ElseBlock>		::= "|!" ( "[" <Ident> "]" )? <BodyBlock> (* Catch *)

(*== ExitCotrol in only end of BodyBlock ==*)
<ExitControl>	::= "^" <Int>?                       (* defer only Hash *)
				| "v"                                (* Continue *)
				| "x"                                (* Break / Terminate *)
				| ">" <Expression>?                (* Return / Output *)

(* =========================================================
	ITERATION
	========================================================= *)

<IterateBlock>	::= <IterSource> "*" <BodyBlock>
<IterSource>	::= <LValue> | <Int> | <Range> | "[" <Expression> "]"

<Range>			::= <Expression> ".." <Expression>

(* =========================================================
	ACTIONS & OPERATIONS (物理演算)
	========================================================= *)

<Action>		::= <LValue> "[" <GeoOp> <Expression>? "]"
 
(*== GeoOp only in Action ==*)
<GeoOp>			::= "<" (* 流し込み (代入) *)
				| "+" | "-" | "*" | "/" | "%" (算術演算)
				| "x" (* 削除  *)
				| "^" | "v" | "_" (* ソート / 待機 *)

<TaskCall>		::= <Ident> "&" "[" <ArgList>? "]"
<PureCall>		::= <Ident> "!" "[" <ArgList>? "]"
<ArgList>		::= <Expression> ( "," <Expression> )*

<SapphireCall> ::= <SapphirePrefix> "[" <Namespace> "." <Ident> <ArgList>? "]" <IOGate>?
<SapphirePrefix>	::=  "$" | "!$"
<Namespace>		::= "sys" | "math" | "io" | "net" | "ui" | "file" | "scene" | "audio" | "event"
<IOGate>		::= "[" <IOParts> "]"
<IOParts>		::= <IOPart> ( "," <IOPart> )*
<IOPart> ::= "in[" <Expression> "]" 
           | "out[" <Expression> "]" 
           | "delay[" <Expression> "]" 
           | "halt"

(* =========================================================
	EXPRESSIONS & LITERALS
	========================================================= *)

<Expression>	::= <LogicalOR>
<LogicalOR>		::= <LogicalAND> ( "or" <LogicalAND> )*
<LogicalAND>	::= <Comparison> ( "and" <Comparison> )*

<Comparison>	::= <MathExpr> ( <CompOp> <MathExpr> )*
<CompOp>		::= "eq" | "ne" | "gt" | "lt" | "ge" | "le"

<MathExpr>		::= <Term> ( ( "+" | "-" ) <Term> )*
<Term>			::= <Factor> ( ( "*" | "/" | "%" ) <Factor> )*

<Factor>		::= <Unary> | <Primary>
<Unary>			::= ( "!" | "-" ) <Primary>

<Primary>		::= <Literal> 
				| <LValue> 
				| <PureCall>      (* 副作用禁止 *)
				| <SapphireCall> 
				| "[" <Expression> "]"

<Literal>		::= <Int> | <Float> | <String> | <Bool> | "null"
<Int>			::= [0-9]+
<Float>			::= [0-9]+ "." [0-9]+
<String>		::= "\"" [^"]* "\""
<Bool>			::= "true" | "false"

<Ident>			::= [a-zA-Z_][a-zA-Z0-9_]*
